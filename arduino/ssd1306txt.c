/*
 * ssd1306txt.c: Write text to 128x64 SSD1306-compatible OLED screens.
 *
 * The font is from lib/fonts/font_mini_4x6.c in the Linux kernel, originally
 * by Kenneth Albanowski. It requires 24 bits per character, for a total of
 * 288 bytes for the 95 printable ASCII characters.
 *
 * Datasheet: https://wiki.52pi.com/images/3/32/SSD1306-Revision_1.0.pdf
 *
 * No rights reserved, released to the public domain.
 * Written by Calvin Owens <jcalvinowens@gmail.com>
 */

#include <Wire.h>

static const int font_width_px = 4;

struct fontchar {
	uint8_t r0 : 4;
	uint8_t r1 : 4;
	uint8_t r2 : 4;
	uint8_t r3 : 4;
	uint8_t r4 : 4;
	uint8_t r5 : 4;
};

static const PROGMEM struct fontchar fonttable[96] = {
	{0x0, 0x0, 0x0, 0x0, 0x0, 0x0}, {0x4, 0x4, 0x4, 0x0, 0x4, 0x0},
	{0xa, 0xa, 0x0, 0x0, 0x0, 0x0}, {0xa, 0xf, 0xf, 0xa, 0x0, 0x0},
	{0x4, 0x6, 0xe, 0xc, 0x4, 0x0}, {0xa, 0x2, 0x4, 0x8, 0xa, 0x0},
	{0x6, 0x9, 0x6, 0xa, 0xd, 0x0}, {0x2, 0x4, 0x0, 0x0, 0x0, 0x0},
	{0x2, 0x4, 0x4, 0x4, 0x2, 0x0}, {0x4, 0x2, 0x2, 0x2, 0x4, 0x0},
	{0x0, 0xe, 0xe, 0xe, 0x0, 0x0}, {0x0, 0x4, 0xe, 0x4, 0x0, 0x0},
	{0x0, 0x0, 0x0, 0x4, 0x8, 0x0}, {0x0, 0x0, 0xe, 0x0, 0x0, 0x0},
	{0x0, 0x0, 0x0, 0x0, 0x4, 0x0}, {0x0, 0x2, 0x4, 0x8, 0x0, 0x0},
	{0x4, 0xa, 0xa, 0xa, 0x4, 0x0}, {0x4, 0xc, 0x4, 0x4, 0xe, 0x0},
	{0xc, 0x2, 0x4, 0x8, 0xe, 0x0}, {0xe, 0x2, 0x6, 0x2, 0xe, 0x0},
	{0xa, 0xa, 0xe, 0x2, 0x2, 0x0}, {0xe, 0x8, 0xe, 0x2, 0xe, 0x0},
	{0xe, 0x8, 0xe, 0xa, 0xe, 0x0}, {0xe, 0x2, 0x2, 0x2, 0x2, 0x0},
	{0xe, 0xa, 0xe, 0xa, 0xe, 0x0}, {0xe, 0xa, 0xe, 0x2, 0x2, 0x0},
	{0x0, 0x0, 0x4, 0x0, 0x4, 0x0}, {0x0, 0x0, 0x4, 0x0, 0x4, 0x8},
	{0x2, 0x4, 0x8, 0x4, 0x2, 0x0}, {0x0, 0xe, 0x0, 0xe, 0x0, 0x0},
	{0x8, 0x4, 0x2, 0x4, 0x8, 0x0}, {0xe, 0x2, 0x6, 0x0, 0x4, 0x0},
	{0x4, 0xe, 0xe, 0x8, 0x4, 0x0}, {0x4, 0xa, 0xe, 0xa, 0xa, 0x0},
	{0xc, 0xa, 0xc, 0xa, 0xc, 0x0}, {0x6, 0x8, 0x8, 0x8, 0x6, 0x0},
	{0xc, 0xa, 0xa, 0xa, 0xc, 0x0}, {0xe, 0x8, 0xe, 0x8, 0xe, 0x0},
	{0xe, 0x8, 0xe, 0x8, 0x8, 0x0}, {0x6, 0x8, 0xe, 0xa, 0x6, 0x0},
	{0xa, 0xa, 0xe, 0xa, 0xa, 0x0}, {0xe, 0x4, 0x4, 0x4, 0xe, 0x0},
	{0x2, 0x2, 0x2, 0xa, 0x4, 0x0}, {0xa, 0xa, 0xc, 0xa, 0xa, 0x0},
	{0x8, 0x8, 0x8, 0x8, 0xe, 0x0}, {0xa, 0xe, 0xe, 0xa, 0xa, 0x0},
	{0xa, 0xe, 0xe, 0xe, 0xa, 0x0}, {0x4, 0xa, 0xa, 0xa, 0x4, 0x0},
	{0xc, 0xa, 0xc, 0x8, 0x8, 0x0}, {0x4, 0xa, 0xa, 0xe, 0x6, 0x0},
	{0xc, 0xa, 0xe, 0xc, 0xa, 0x0}, {0x6, 0x8, 0x4, 0x2, 0xc, 0x0},
	{0xe, 0x4, 0x4, 0x4, 0x4, 0x0}, {0xa, 0xa, 0xa, 0xa, 0x6, 0x0},
	{0xa, 0xa, 0xa, 0x4, 0x4, 0x0}, {0xa, 0xa, 0xe, 0xe, 0xa, 0x0},
	{0xa, 0xa, 0x4, 0xa, 0xa, 0x0}, {0xa, 0xa, 0x4, 0x4, 0x4, 0x0},
	{0xe, 0x2, 0x4, 0x8, 0xe, 0x0}, {0x6, 0x4, 0x4, 0x4, 0x6, 0x0},
	{0x0, 0x8, 0x4, 0x2, 0x0, 0x0}, {0x6, 0x2, 0x2, 0x2, 0x6, 0x0},
	{0x4, 0xa, 0x0, 0x0, 0x0, 0x0}, {0x0, 0x0, 0x0, 0x0, 0x0, 0xf},
	{0x8, 0x4, 0x0, 0x0, 0x0, 0x0}, {0x0, 0x0, 0x6, 0xa, 0xe, 0x0},
	{0x8, 0x8, 0xc, 0xa, 0xc, 0x0}, {0x0, 0x0, 0x6, 0x8, 0x6, 0x0},
	{0x2, 0x2, 0x6, 0xa, 0x6, 0x0}, {0x0, 0xe, 0xe, 0x8, 0x6, 0x0},
	{0x2, 0x4, 0xe, 0x4, 0x4, 0x0}, {0x0, 0x6, 0xa, 0x6, 0xe, 0x0},
	{0x8, 0x8, 0xc, 0xa, 0xa, 0x0}, {0x4, 0x0, 0x4, 0x4, 0x4, 0x0},
	{0x4, 0x0, 0x4, 0x4, 0x8, 0x0}, {0x0, 0x8, 0xa, 0xc, 0xa, 0x0},
	{0x0, 0xc, 0x4, 0x4, 0xe, 0x0}, {0x0, 0x0, 0xe, 0xe, 0xa, 0x0},
	{0x0, 0x0, 0xc, 0xa, 0xa, 0x0}, {0x0, 0x4, 0xa, 0xa, 0x4, 0x0},
	{0x0, 0x0, 0xc, 0xa, 0xc, 0x8}, {0x0, 0x0, 0x6, 0xa, 0x6, 0x2},
	{0x0, 0xc, 0xa, 0x8, 0x8, 0x0}, {0x0, 0x6, 0xc, 0x2, 0xc, 0x0},
	{0x0, 0x4, 0xe, 0x4, 0x4, 0x0}, {0x0, 0x0, 0xa, 0xa, 0x6, 0x0},
	{0x0, 0x0, 0xa, 0xe, 0x4, 0x0}, {0x0, 0x0, 0xa, 0xe, 0xe, 0x0},
	{0x0, 0x0, 0xa, 0x4, 0xa, 0x0}, {0x0, 0x0, 0xa, 0xe, 0x2, 0xc},
	{0x0, 0xe, 0x6, 0xc, 0xe, 0x0}, {0x2, 0x4, 0xc, 0x4, 0x2, 0x0},
	{0x4, 0x4, 0x4, 0x4, 0x4, 0x0}, {0x8, 0x4, 0x6, 0x4, 0x8, 0x0},
	{0x5, 0xa, 0x0, 0x0, 0x0, 0x0}, {0xf, 0x9, 0x9, 0x9, 0x9, 0xf},
};

static const int ssd1306_i2c_addr = 0x3C;
static const int ssd1306_width_px = 128;
static const int ssd1306_nr_pages = 8;
static const int ssd1306_chars = ssd1306_width_px / font_width_px;

static void ssd1306_i2c(const uint8_t *cmd, int len, bool data)
{
	int i;

	Wire.beginTransmission(ssd1306_i2c_addr);
	Wire.write(byte(data ? 0x40 : 0x00));

	for (i = 0; i < len; i++)
		Wire.write(cmd[i]);

	Wire.endTransmission();
}

static void ssd1306_init(void)
{
	const uint8_t ssd1306_init_sequence[] = {
		0xAE, 0xD5, 0x80, 0xA8, 0x3F, 0xD3, 0x00, 0x40,
		0x8D, 0x14, 0x20, 0x02, 0xA1, 0xC8, 0xDA, 0x12,
		0x81, 0xCF, 0xD9, 0xF1, 0xDB, 0x40, 0xA4, 0xA6,
		0x2E, 0xAF,
	};

	ssd1306_i2c(ssd1306_init_sequence, sizeof(ssd1306_init_sequence), false);
}

static void ssd1306_set_active_page(uint8_t page)
{
	uint8_t cmd[3] = {0xB0, 0x00, 0x10};

	cmd[0] |= page;
	ssd1306_i2c(cmd, sizeof(cmd), false);
}

static void ssd1306_clear_page(uint8_t page)
{
	uint8_t cmd[16] = {0};
	int i;

	ssd1306_set_active_page(page);
	for (i = 0; i < ssd1306_width_px / sizeof(cmd); i++)
		ssd1306_i2c(cmd, sizeof(cmd), true);
}

static void ssd1306_clear(void)
{
	int i;

	for (i = 0; i < ssd1306_nr_pages; i++)
		ssd1306_clear_page(i);
}

static void ssd1306_get_character(char c, uint8_t *out)
{
	struct fontchar desc;
	int i;

	if (c < ' ' || c > '~')
		c = 127;

	memcpy_P(&desc, fonttable + c - ' ', sizeof(desc));

	for (i = 0; i < font_width_px; i++) {
		uint8_t mask = 1 << (font_width_px - i - 1);

		out[i] |= !!(desc.r0 & mask) << 1;
		out[i] |= !!(desc.r1 & mask) << 2;
		out[i] |= !!(desc.r2 & mask) << 3;
		out[i] |= !!(desc.r3 & mask) << 4;
		out[i] |= !!(desc.r4 & mask) << 5;
		out[i] |= !!(desc.r5 & mask) << 6;
	}
}

static void ssd1306_text(int pg_row, const char *txt)
{
	uint8_t cmd[font_width_px * ssd1306_chars] = {0};
	int i;

	for (i = 0; i < ssd1306_chars; i++) {
		if (!txt[i])
			break;

		ssd1306_get_character(txt[i], cmd + i * font_width_px);
	}

	ssd1306_set_active_page(pg_row);
	ssd1306_i2c(cmd, i * font_width_px, true);
}

void setup(void)
{
	Wire.begin();
	Wire.setClock(400000);
	ssd1306_init();
	ssd1306_clear();
}

void loop(void)
{
	int i, j;

	for (i = 0; i < 3; i++) {
		char txt[ssd1306_chars];

		for (j = 0; j < ssd1306_chars; j++)
			txt[j] = ' ' + i * ssd1306_chars + j;

		ssd1306_text(i, txt);
	}

	delay(1000);
	ssd1306_text(5, "          HELLO WORLD!          ");

	delay(1000);
	ssd1306_text(4, "********************************");
	ssd1306_text(6, "********************************");

	delay(30000);
	ssd1306_clear();
}
